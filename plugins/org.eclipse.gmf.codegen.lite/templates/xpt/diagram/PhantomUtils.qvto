/*
 * Copyright (c) 2007, 2010 Borland Software Corporation and others
 * 
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *    bblajer (Borland) - initial API and implementation
 */

import xpt.GenModelUtils;
import gmf.GenModelUtils;

modeltype genmodel uses "http://www.eclipse.org/emf/2002/GenModel";
modeltype gmfgen uses "http://www.eclipse.org/gmf/2009/GenModel";

library PhantomUtils;

helper getPhantomNodes(diagram : gmfgen::GenDiagram) : Collection(gmfgen::GenTopLevelNode) {
	return diagram.topLevelNodes->select(e | e.modelFacet.isPhantomElement())
}

helper gmfgen::GenDiagram::getPhantomFeatures() : Collection(genmodel::GenFeature) {
	return getPhantomFeatures(self, getPhantomNodes(self))
}

helper gmfgen::GenTopLevelNode::getPhantomFeatures() : Collection(genmodel::GenFeature) {
	return getPhantomFeatures(self.getDiagram(), Sequence { self })
}

helper getPhantomFeatures(diagram : gmfgen::GenDiagram, phantomNodes : Collection(gmfgen::GenTopLevelNode)) : Collection(genmodel::GenFeature) {
	return diagram.links->select(e | e.modelFacet.isPhantomLink(phantomNodes))->collect(e | e.modelFacet.oclAsType(gmfgen::FeatureLinkModelFacet).metaFeature)
}

helper gmfgen::LinkModelFacet::isPhantomLink(diagram : gmfgen::GenDiagram) : Boolean {
	return self.isPhantomLink(getPhantomNodes(diagram))
}

helper gmfgen::LinkModelFacet::isPhantomLink(phantoms : Collection(gmfgen::GenTopLevelNode)) : Boolean {
	return false
}

helper gmfgen::FeatureLinkModelFacet::isPhantomLink(phantoms : Collection(gmfgen::GenTopLevelNode)) : Boolean {
	return (if not isContains(self.metaFeature) then false else let featureType = getTypeGenClassX(self.metaFeature) in phantoms->select(e | featureType.isSuperTypeOf(e.modelFacet.metaClass))->size() > 0 endif)
}
