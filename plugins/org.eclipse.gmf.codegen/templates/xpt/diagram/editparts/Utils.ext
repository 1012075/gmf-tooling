import "http://www.eclipse.org/gmf/2005/GenModel/2.0";
import "http://www.eclipse.org/emf/2002/Ecore";

Boolean isStoringChildPositions(gmfgen::GenNode node) :
	 node.getLayoutType().value == gmfgen::ViewmapLayoutType::XY_LAYOUT.value
;

Boolean hasExternalLabels(gmfgen::GenNode node) :
	node.labels.typeSelect(gmfgen::GenExternalNodeLabel).size() > 0
;

List[gmfgen::GenChildSideAffixedNode] getSideAffixedChildren(gmfgen::GenNode node) :
	node.childNodes.typeSelect(gmfgen::GenChildSideAffixedNode)
;

List[gmfgen::GenLabel] getInnerFixedLabels(gmfgen::GenNode node) :
	node.labels.without(node.labels.typeSelect(gmfgen::GenExternalNodeLabel)).
		select(e | { ((gmfgen::GenLabel) e).viewmap }.typeSelect(gmfgen::ParentAssignedViewmap).size() > 0)
;

List[gmfgen::GenCompartment] getPinnedCompartments(gmfgen::GenNode node) :
	node.compartments.select(e | { e.viewmap }.typeSelect(gmfgen::ParentAssignedViewmap).size() > 0)
;

Boolean hasFixedChildren(gmfgen::GenNode node) :
	getInnerFixedLabels(node).size() > 0 || getPinnedCompartments(node).size() > 0
;

Boolean hasChildren(gmfgen::GenNode node) :
	!node.childNodes.isEmpty()
;

Boolean listCompartmentHasChildren(gmfgen::GenCompartment compartment) :
	compartment.listLayout && compartment.childNodes.size() > 0
;

Boolean hasChildrenInListCompartments(gmfgen::GenNode node) :
	node.compartments.exists(e | listCompartmentHasChildren(e))
;

String nodeEditPartSuperClass(gmfgen::GenNode node) :
	getSideAffixedChildren(node).size() > 0 ?
		"org.eclipse.gmf.runtime.diagram.ui.editparts.AbstractBorderedShapeEditPart" :
		"org.eclipse.gmf.runtime.diagram.ui.editparts.ShapeNodeEditPart"
;

String nodeEditPartSuperClass(gmfgen::GenChildSideAffixedNode node) :
	getSideAffixedChildren(node).size() > 0 ?
		"org.eclipse.gmf.runtime.diagram.ui.editparts.BorderedBorderItemEditPart" :
		"org.eclipse.gmf.runtime.diagram.ui.editparts.AbstractBorderItemEditPart"
;

Boolean needsGraphicalNodeEditPolicy(gmfgen::GenNode node) :
	null != node.modelFacet && node.reorientedIncomingLinks.size() > 0
;

String getSetterName(gmfgen::ParentAssignedViewmap viewmap) :
	viewmap.setterName == null ? "setLabel" : viewmap.setterName
;
