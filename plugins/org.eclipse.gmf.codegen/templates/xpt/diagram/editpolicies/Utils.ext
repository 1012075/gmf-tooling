/*
 * Copyright (c) 2006 Borland Software Corporation
 * 
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *    Alexander Shatalin (Borland) - initial API and implementation
 */
 
import "http://www.eclipse.org/gmf/2005/GenModel/2.0";
import "http://www.eclipse.org/emf/2002/GenModel";
import "http://www.eclipse.org/emf/2002/Ecore";
extension xpt::GenModelUtils;

genmodel::GenClass getModelElementType(gmfgen::GenCommonBase commonBase) :
	null
;

genmodel::GenClass getModelElementType(gmfgen::GenDiagram diagram) :
	diagram.domainDiagramElement
;

genmodel::GenClass getModelElementType(gmfgen::GenCompartment compartment) :
	getModelElementType(compartment.node)
;

genmodel::GenClass getModelElementType(gmfgen::GenNode node) :
	node.modelFacet.metaClass
;

cached List[gmfgen::GenNode] getConnectedPhantomNodes(gmfgen::TypeModelFacet typeModelFacet, gmfgen::GenDiagram diagram) :
	getPhantomNodes(diagram).select(node | canBePhantomLinkTarget(node, typeModelFacet, diagram))
;

cached List[gmfgen::GenNode] getPhantomNodes(gmfgen::GenDiagram diagram) :
	diagram.topLevelNodes.select(node | null != node.modelFacet && node.modelFacet.isPhantomElement())
;

private boolean canBePhantomLinkTarget(gmfgen::GenNode node, gmfgen::TypeModelFacet typeModelFacet, gmfgen::GenDiagram diagram) :
	!getOutgoingPhantomLinks(typeModelFacet, diagram).select(link | canBeTarget(link, node.modelFacet)).isEmpty()
;

cached List[gmfgen::GenLink] getOutgoingPhantomLinks(gmfgen::TypeModelFacet typeModelFacet, gmfgen::GenDiagram diagram) :
	diagram.links.select(link | canBeSource(link, typeModelFacet) && isPhantom(link.modelFacet))
;

cached List[gmfgen::GenLink] getContainedLinks(gmfgen::TypeModelFacet typeModelFacet, gmfgen::GenDiagram diagram) :
	diagram.links.select(link | canBeContainer(link, typeModelFacet))
;

private boolean isPhantom(gmfgen::FeatureLinkModelFacet modelFacet) :
	modelFacet.metaFeature.isContains()
;

private boolean isPhantom(gmfgen::LinkModelFacet modelFacet) :
	false
;

cached Set[genmodel::GenFeature] getChildFeatures(List[gmfgen::GenNode] nodes) :
	selectSemanticChildren(nodes).collect(node | node.modelFacet.childMetaFeature).toSet()
;

cached List[gmfgen::GenNode] selectSemanticChildren(List[gmfgen::GenNode] nodes) :
	nodes.select(node | null != node.modelFacet && !node.modelFacet.isPhantomElement())
;

cached List[gmfgen::GenNode] selectNotationChildren(List[gmfgen::GenNode] nodes) :
	nodes.select(node | null == node.modelFacet || !node.modelFacet.isPhantomElement())
;

boolean hasChildrenOrCompartments(gmfgen::GenNode node) :
	!node.childNodes.isEmpty() || !node.compartments.isEmpty()
;

String getContainerVariable(gmfgen::TypeLinkModelFacet modelFacet) :
	null != modelFacet.sourceMetaFeature
		? "container"
		: "source"
;

boolean hasJavaConstraints(gmfgen::GenDiagram diagram) :
	!getAllExpressionProviders(getValidLinkConstraints(diagram), getExpressionProvider(diagram)).typeSelect(gmfgen::GenJavaExpressionProvider).isEmpty()
;

boolean hasOCLConstraints(gmfgen::GenDiagram diagram) :
	!getAllExpressionProviders(getValidLinkConstraints(diagram), getExpressionProvider(diagram)).typeSelect(gmfgen::GenExpressionInterpreter).isEmpty()
;

private gmfgen::GenExpressionProviderContainer getExpressionProvider(gmfgen::GenDiagram diagram) :
	diagram.editorGen.expressionProviders
;

private List[gmfgen::GenExpressionProviderBase] getAllExpressionProviders(List[gmfgen::GenLinkConstraints] constraints, gmfgen::GenExpressionProviderContainer providersContainer) :
	constraints.collect(c | c.sourceEnd).addAll(constraints.collect(c | c.targetEnd)).select(c | null != c).typeSelect(gmfgen::GenConstraint).collect(c | providersContainer.getProvider(c))
;

String getTargetExpressionVarName(gmfgen::GenLink link) :
	link.getUniqueIdentifier() + "_TargetExpression"
;

String getSourceExpressionVarName(gmfgen::GenLink link) :
	link.getUniqueIdentifier() + "_SourceExpression"
;

List[gmfgen::GenLinkConstraints] getValidLinkConstraints(gmfgen::GenDiagram diagram) :
	diagram.links.select(l | null != l.creationConstraints && l.creationConstraints.isValid()).collect(l | l.creationConstraints)
;

List[gmfgen::GenLink] getAllPotentialLinks(gmfgen::TypeModelFacet typeModelFacet, gmfgen::GenDiagram diagram) :
	selectValidLinks(diagram.links).select(l | isSelf(l, typeModelFacet) || isOutgoing(l, typeModelFacet) || isIncoming(l, typeModelFacet))
;

List[gmfgen::GenLink] getReroutableTypeLinks(gmfgen::TypeModelFacet typeModelFacet, gmfgen::GenDiagram diagram) :
	diagram.links.select(link | isTypeLink(link) && (canBeSource(link, typeModelFacet) || canBeTarget(link, typeModelFacet)))
;

List[gmfgen::GenLink] getReroutableRefLinks(gmfgen::TypeModelFacet typeModelFacet, gmfgen::GenDiagram diagram) :
	diagram.links.select(link | isRefLink(link) && (canBeSource(link, typeModelFacet) || canBeTarget(link, typeModelFacet)))
;

private boolean isTypeLink(gmfgen::GenLink link) :
	{link.modelFacet}.typeSelect(gmfgen::TypeLinkModelFacet).size() > 0
;

private boolean isRefLink(gmfgen::GenLink link) :
	{link.modelFacet}.typeSelect(gmfgen::FeatureLinkModelFacet).size() > 0
;

private List[gmfgen::GenLink] selectValidLinks(List[gmfgen::GenLink] allLinks) :
	allLinks.select(l | null != l.modelFacet)
		.select(l | l.outgoingCreationAllowed || l.incomingCreationAllowed)
;

boolean createStartLinkCommand(gmfgen::GenLink link, gmfgen::TypeModelFacet typeModelFacet) :
	createStartOutgoingLinkCommand(link, typeModelFacet) || createStartIncomingLinkCommand(link, typeModelFacet)
;

boolean createStartOutgoingLinkCommand(gmfgen::GenLink link, gmfgen::TypeModelFacet typeModelFacet) :
	isSelf(link, typeModelFacet) || (isOutgoing(link, typeModelFacet) && link.outgoingCreationAllowed)
;

boolean createStartIncomingLinkCommand(gmfgen::GenLink link, gmfgen::TypeModelFacet typeModelFacet) :
	isIncoming(link, typeModelFacet) && link.incomingCreationAllowed
;

boolean createCompleteLinkCommand(gmfgen::GenLink link, gmfgen::TypeModelFacet typeModelFacet) :
	createCompleteIncomingLinkCommand(link, typeModelFacet) || createCompleteOutgoingLinkCommand(link, typeModelFacet)
;

boolean createCompleteIncomingLinkCommand(gmfgen::GenLink link, gmfgen::TypeModelFacet typeModelFacet) :
	isSelf(link, typeModelFacet) || (isIncoming(link, typeModelFacet) && link.outgoingCreationAllowed)
;

boolean createCompleteOutgoingLinkCommand(gmfgen::GenLink link, gmfgen::TypeModelFacet typeModelFacet) :
	isOutgoing(link, typeModelFacet) && link.incomingCreationAllowed
;

boolean checkSource(boolean reversedRequest, boolean isCompleteCommand) :
	!reversedRequest || isCompleteCommand
;

boolean checkTarget(boolean reversedRequest, boolean isCompleteCommand) :
	reversedRequest || isCompleteCommand
;

private boolean isSelf(gmfgen::GenLink link, gmfgen::TypeModelFacet typeModelFacet) :
	canBeSource(link, typeModelFacet) && canBeTarget(link, typeModelFacet)
;

private boolean isOutgoing(gmfgen::GenLink link, gmfgen::TypeModelFacet typeModelFacet) :
	canBeSource(link, typeModelFacet) && !isSelf(link, typeModelFacet)
;

private boolean isIncoming(gmfgen::GenLink link, gmfgen::TypeModelFacet typeModelFacet) :
	canBeTarget(link, typeModelFacet) && !isSelf(link, typeModelFacet)
;

private boolean canBeSource(gmfgen::GenLink link, gmfgen::TypeModelFacet typeModelFacet) :
	null != link.modelFacet && canBeLinkEnd(link.modelFacet.getSourceType(), typeModelFacet)
;

private boolean canBeTarget(gmfgen::GenLink link, gmfgen::TypeModelFacet typeModelFacet) :
	null != link.modelFacet && canBeLinkEnd(link.modelFacet.getTargetType(), typeModelFacet)
;

boolean canBeContainer(gmfgen::GenLink link, gmfgen::TypeModelFacet typeModelFacet) :
	null != link.modelFacet && null != getContainerClass(link.modelFacet) && canBeLinkEnd(getContainerClass(link.modelFacet), typeModelFacet)
;

genmodel::GenClass getContainerClass(gmfgen::LinkModelFacet modelFacet) :
// Should not be called
	null
;

genmodel::GenClass getContainerClass(gmfgen::TypeLinkModelFacet modelFacet) :
	modelFacet.containmentMetaFeature.genClass
;

genmodel::GenClass getContainerClass(gmfgen::FeatureLinkModelFacet modelFacet) :
	modelFacet.metaFeature.genClass
;

private boolean canBeLinkEnd(genmodel::GenClass endType, gmfgen::TypeModelFacet typeModelFacet) :
	null != endType && endType.ecoreClass.isSuperTypeOf(getMetaclass(typeModelFacet))
;

private ecore::EClass getMetaclass(gmfgen::TypeModelFacet typeModelFacet) :
	typeModelFacet.metaClass.ecoreClass
;

String i18nKeyForLinkConstraintErrorLog() :
"EvaluateOCLLinkConstraintError"
;

String i18nKeyForOpenCommandName() :
"CommandName.OpenDiagram"
;
