/*
 * Copyright (c) 2006 Borland Software Corporation
 * 
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *    Dmitry Stadnik (Borland) - initial API and implementation
 */

import "http://www.eclipse.org/gmf/2005/GenModel/2.0";
import "http://www.eclipse.org/emf/2002/Ecore";

Boolean isFixedFont(gmfgen::Viewmap viewmap) :
	null != viewmap && viewmap.attributes.typeSelect(gmfgen::StyleAttributes).exists(e | e.fixedFont)
;

gmfgen::ResizeConstraints getResizeConstraints(gmfgen::Viewmap viewmap) :
	null == viewmap ? null : ((gmfgen::ResizeConstraints) viewmap.attributes.typeSelect(gmfgen::ResizeConstraints).first())
;

gmfgen::DefaultSizeAttributes getDefaultSizeAttributes(gmfgen::Viewmap viewmap) :
	null == viewmap ? null : ((gmfgen::DefaultSizeAttributes) viewmap.attributes.typeSelect(gmfgen::DefaultSizeAttributes).first())
;

Integer defaultSizeWidth(gmfgen::Viewmap viewmap, Integer defaultValue) :
	let a = getDefaultSizeAttributes(viewmap) :
		(null == a ? defaultValue : a.width)
;

Integer defaultSizeHeight(gmfgen::Viewmap viewmap, Integer defaultValue) :
	let a = getDefaultSizeAttributes(viewmap) :
		(null == a ? defaultValue : a.height)
;

gmfgen::LabelOffsetAttributes getLabelOffsetAttributes(gmfgen::Viewmap viewmap) :
	null == viewmap ? null : ((gmfgen::LabelOffsetAttributes) viewmap.attributes.typeSelect(gmfgen::LabelOffsetAttributes).first())
;

Integer labelOffsetX(gmfgen::Viewmap viewmap, Integer defaultValue) :
	let a = getLabelOffsetAttributes(viewmap) :
		(null == a ? defaultValue : a.x)
;

Integer labelOffsetY(gmfgen::Viewmap viewmap, Integer defaultValue) :
	let a = getLabelOffsetAttributes(viewmap) :
		(null == a ? defaultValue : a.y)
;
